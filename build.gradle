plugins {
	id 'java'
	id 'eclipse'
	id 'org.springframework.boot' version '2.1.1.RELEASE'
	id "com.palantir.docker" version "0.20.1"
	id 'com.avast.gradle.docker-compose' version '0.7.1'
}

group = 'org.adhuc.cena'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 11

repositories {
	mavenCentral()
}

sourceSets {
	acceptance {
		java {
			compileClasspath += main.compileClasspath + test.compileClasspath + main.output + test.output
			runtimeClasspath += main.runtimeClasspath + test.runtimeClasspath + main.output + test.output
			srcDir file('src/acceptance/java')
		}
		resources.srcDir file('src/acceptance/resources')
	}
}

configurations {
	acceptanceTestCompile.extendsFrom testCompile
	acceptanceTestRuntime.extendsFrom testRuntime
}

def moduleName = 'org.adhuc.cena.menu.generation'
def springBootVersion = '2.1.1.RELEASE'
def springBootAdminVersion = '2.1.1'

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
	implementation platform("de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}")
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-hateoas')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('de.codecentric:spring-boot-admin-starter-client')
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	compileOnly('org.projectlombok:lombok')
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('org.hamcrest:hamcrest:2.1')
	testImplementation('org.hamcrest:hamcrest-library:2.1')
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
}

configurations {
	implementation {
		exclude group: 'com.google.code.findbugs', module: 'jsr305'
		exclude group: 'org.apache.tomcat', module: 'tomcat-annotations-api'
	}
	testImplementation {
		exclude group: 'com.vaadin.external.google', module: 'android-json'
		exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-core'
	}
}

compileJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs = [
				'--module-path', classpath.asPath,
		]
		classpath = files()
	}
}

compileTestJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs = [
				'--module-path', classpath.asPath,
				'--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
		]
		classpath = files()
	}
}

test {
	useJUnitPlatform()
}

task acceptance(type: Test) {
	useJUnitPlatform()
	outputs.upToDateWhen { false }
	testClassesDirs = sourceSets.acceptance.output.classesDirs
	classpath = sourceSets.acceptance.runtimeClasspath
	doFirst { dockerCompose.exposeAsSystemProperties(acceptance) }
}
dockerCompose.isRequiredBy(acceptance)

bootJar {
	archiveName = "${rootProject.name}.jar"
}

docker {
	name "${rootProject.name}:latest"
	files tasks.bootJar.outputs
	copySpec.into("build")
}

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}
