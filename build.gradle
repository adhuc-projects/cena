buildscript {
	repositories {
		mavenCentral()
	}
	configurations.maybeCreate("pitest")
	dependencies {
		classpath('net.serenity-bdd:serenity-gradle-plugin:2.0.81')
		classpath('info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.5')
		pitest('org.pitest:pitest-junit5-plugin:0.8')
	}
}

plugins {
	id 'java'
	id 'eclipse'
	id 'org.springframework.boot' version '2.2.0.RELEASE'
	id 'com.palantir.docker' version '0.22.1'
	id 'com.avast.gradle.docker-compose' version '0.9.5'
	id 'org.asciidoctor.convert' version '1.5.9.2'
	id 'io.franzbecker.gradle-lombok' version '3.2.0'
	id 'jacoco'
	id 'info.solidsoft.pitest' version '1.4.5'
	id 'com.github.hierynomus.license' version '0.15.0'
}
apply plugin: 'net.serenity-bdd.aggregator'

group = 'org.adhuc.cena'
version = '0.2.0-SNAPSHOT'
sourceCompatibility = 11

repositories {
	mavenCentral()
}

sourceSets {
	acceptance {
		java {
			compileClasspath += test.compileClasspath + test.output
			runtimeClasspath += test.runtimeClasspath + test.output
			srcDir file('src/acceptance/java')
		}
		resources.srcDir file('src/acceptance/resources')
	}
}

configurations {
	acceptanceTestCompile.extendsFrom testCompile
	acceptanceTestRuntime.extendsFrom testRuntime
}

def moduleName = 'org.adhuc.cena.menu'
def springBootVersion = '2.2.0.RELEASE'
def springRestDocsVersion = '2.0.4.RELEASE'
def lombokVersion = '1.18.10'
def swaggerVersion = '1.5.24'
def swaggerValidatorVersion = '2.8.0'
def archUnitVersion = '0.11.0'
def restAssuredVersion = '4.1.2'
def serenityVersion = '2.0.81'
def serenityCucumberVersion = '1.9.45'
def snippetsDir = file('build/generated-snippets')

dependencies {
	implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-hateoas')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('org.springframework.boot:spring-boot-starter-security')
	implementation("commons-io:commons-io:2.6")
	implementation("io.swagger:swagger-core:${swaggerVersion}")
	implementation("com.atlassian.oai:swagger-request-validator-springmvc:${swaggerValidatorVersion}")
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	testCompile("com.tngtech.archunit:archunit-junit5-api:${archUnitVersion}")
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('org.springframework.security:spring-security-test')
	testImplementation('org.springframework.restdocs:spring-restdocs-mockmvc')
	testImplementation('org.junit.jupiter:junit-jupiter-api')
	testImplementation('org.junit.jupiter:junit-jupiter-params')
	testImplementation('org.junit.vintage:junit-vintage-engine')
	testImplementation('org.hamcrest:hamcrest')
	testImplementation("io.rest-assured:rest-assured:${restAssuredVersion}")
	testImplementation("io.rest-assured:rest-assured-all:${restAssuredVersion}")
	testRuntime("com.tngtech.archunit:archunit-junit5-engine:${archUnitVersion}")
	testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine')
	acceptanceCompileOnly("org.projectlombok:lombok:${lombokVersion}")
	acceptanceAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
	acceptanceImplementation("net.serenity-bdd:serenity-core:${serenityVersion}")
	acceptanceImplementation("net.serenity-bdd:serenity-junit:${serenityVersion}")
	acceptanceImplementation("net.serenity-bdd:serenity-rest-assured:${serenityVersion}")
	acceptanceImplementation("net.serenity-bdd:serenity-cucumber:${serenityCucumberVersion}")
	asciidoctor("org.springframework.restdocs:spring-restdocs-asciidoctor:${springRestDocsVersion}")
}

configurations {
	implementation {
		exclude group: 'com.google.code.findbugs', module: 'jsr305'
		exclude group: 'org.apache.tomcat', module: 'tomcat-annotations-api'
		exclude group: 'jakarta.validation', module: 'jakarta.validation-api'
		exclude group: 'org.slf4j', module: 'slf4j-ext'
		exclude group: 'javax.activation', module: 'activation'
		exclude group: 'javax.servlet', module: 'javax.servlet-api'
		exclude group: 'io.swagger.parser.v3', module: 'swagger-parser-v2-converter'
	}
	testImplementation {
		exclude group: 'com.vaadin.external.google', module: 'android-json'
		exclude group: 'org.hamcrest', module: 'hamcrest-core'
		exclude group: 'javax.activation', module: 'javax.activation-api'
		exclude group: 'jakarta.xml.bind', module: 'jakarta.xml.bind-api'
		exclude group: 'org.xmlunit', module: 'xmlunit-core'
		exclude group: 'io.rest-assured', module: 'rest-assured-common'
		exclude group: 'io.rest-assured', module: 'json-path'
		exclude group: 'io.rest-assured', module: 'xml-path'
		exclude group: 'org.codehaus.groovy', module: 'groovy-xml'
	}
}

lombok {
	version = "${lombokVersion}"
	sha256 = ""
}

license {
	header rootProject.file('.license')
	excludes(['**/*.feature', '**/*.yml', '**/*.snippet', '**/*.properties'])
}

compileJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs = [
			'--module-path', classpath.asPath,
		]
		classpath = files()
	}
}

compileTestJava {
	inputs.property("moduleName", moduleName)
	doFirst {
		options.compilerArgs = [
			'--module-path', classpath.asPath,
			'--add-modules', 'org.assertj.core',
			'--add-modules', 'org.hamcrest',
			'--add-modules', 'org.junit.jupiter.api',
			'--add-modules', 'org.junit.jupiter.params',
			'--add-modules', 'org.mockito',
			'--add-modules', 'rest.assured',
			'--add-modules', 'rest.assured.all',
			'--add-reads', "$moduleName=org.assertj.core",
			'--add-reads', "$moduleName=org.hamcrest",
			'--add-reads', "$moduleName=org.junit.jupiter.api",
			'--add-reads', "$moduleName=org.junit.jupiter.params",
			'--add-reads', "$moduleName=org.mockito",
			'--add-reads', "$moduleName=rest.assured",
			'--add-reads', "$moduleName=rest.assured.all",
			'--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
		]
		classpath = files()
	}
}

test {
	useJUnitPlatform()
	outputs.dir snippetsDir
}

pitest {
	testPlugin = 'junit5'
	targetClasses = ['org.adhuc.cena.menu.*']
	outputFormats = ['XML', 'HTML']
	excludedMethods = ['equals', 'hashCode', 'toString']
	timestampedReports = false
	mutationThreshold = 75
}

jacocoTestReport {
	reports {
		xml.enabled true
	}
}

asciidoctor {
	inputs.dir snippetsDir
	dependsOn test
}

task acceptance(type: Test) {
	outputs.upToDateWhen { false }
	testClassesDirs = sourceSets.acceptance.output.classesDirs
	classpath = sourceSets.acceptance.runtimeClasspath
	doFirst { dockerCompose.exposeAsSystemProperties(acceptance) }
	gradle.startParameter.continueOnFailure = true
}
dockerCompose.isRequiredBy(acceptance)
check.dependsOn.remove("checkOutcomes")

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
	from("src/main/resources/api") {
		into 'static/docs'
	}
	archiveFileName = "${rootProject.name}.jar"
}

bootRun {
	// support passing -Dsystem.property=value to bootRun task
	systemProperties = System.properties
}

docker {
	name "${rootProject.name}:latest"
	files tasks.bootJar.outputs
	copySpec.into("build")
}

dockerCompose {
	useComposeFiles = ['docker-compose.yml']
}
